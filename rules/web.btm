HELPER org.jboss.byteman.koubsky.LogHelper

#Logging undertow handlers chain
RULE logServletHandler.handleRequest
CLASS io.undertow.servlet.handlers.ServletHandler
METHOD handleRequest(HttpServerExchange)
AT ENTRY
IF true
DO log($CLASS, "DEBUG", "Method handleRequest, request info: " + 
		" URL = " + $1.getRequestURL() + ", protocol = " + $1.getProtocol() + 
		", method = " + $1.getRequestMethod() + ", query string: " + $1.getQueryString())
ENDRULE

RULE logServletSecurityRoleHandler.handleRequest.canAccess
CLASS io.undertow.servlet.handlers.security.ServletSecurityRoleHandler
METHOD handleRequest(HttpServerExchange)
AT LINE 59
IF true
DO log($CLASS, "DEBUG", "Method handleRequest, user can access a given resource, continuing in handlers chain to ServletHandler.")
ENDRULE

RULE logServletSecurityRoleHandler.handleRequest.SC_FORBIDDEN
CLASS io.undertow.servlet.handlers.security.ServletSecurityRoleHandler
METHOD handleRequest(HttpServerExchange)
AT LINE 57
IF true
DO log($CLASS, "DEBUG", "Method handleRequest, user cannot access a given resource, sending SC_FORBIDDEN(403).")
ENDRULE

RULE logServletDispatchingHandler.handleRequest
CLASS io.undertow.servlet.handlers.ServletDispatchingHandler
METHOD handleRequest(HttpServerExchange)
AT ENTRY
IF true
DO log($CLASS, "DEBUG", "Method handleRequest, dispatching to the resolved servlet: " + $1.getRelativePath())
ENDRULE

RULE logSecurityContextAssociationHandler.handleRequest
CLASS org.wildfly.extension.undertow.security.SecurityContextAssociationHandler
METHOD handleRequest(HttpServerExchange)
AT LINE 75
BIND scString = $sc == null ? "null" : "Security domain = " + $sc.getSecurityDomain() + ", Subject info: " + $sc.getSubjectInfo() + 
				", context map = " + $sc.getData() + ", incomingRunAs = " + $sc.getIncomingRunAs() + ", outgoingRunAs = " + $sc.getOutgoingRunAs();
IF true
DO log($CLASS, "DEBUG", "Method handleRequest, trying to set RunAsIdentity: \n RunAsIdentity = " + $runAsIdentity
		+ "; \n SecurityContext: " + scString)
ENDRULE

RULE logPredicateHandler.handleRequest
CLASS io.undertow.server.handlers.PredicateHandler
METHOD handleRequest(HttpServerExchange)
AT LINE 43
IF true
DO log($CLASS, "DEBUG", "Method handleRequest, resolving predicates for request in HttpServerExchange. \n TrueHandler = " + $0.trueHandler
		+ " \n FalseHandler = " + $0.falseHandler + " \n Resulted handler: " + $next)
ENDRULE

RULE logSSLInformationAssociationHandler.handleRequest.addSSLAttributes
CLASS io.undertow.servlet.handlers.security.SSLInformationAssociationHandler
METHOD handleRequest(HttpServerExchange)
AT LINE 122
IF true
DO log($CLASS, "DEBUG", "Method handleRequest, adding SSL attributes to request: \n" 
		+ "javax.servlet.request.cipher_suite = " + $request.getAttribute("javax.servlet.request.cipher_suite")
		+ "javax.servlet.request.key_size = " + $request.getAttribute("javax.servlet.request.key_size")
		+ "javax.servlet.request.ssl_session_id = " + $request.getAttribute("javax.servlet.request.ssl_session_id")
		)
ENDRULE

RULE logSSLInformationAssociationHandler.handleRequest.addCertificatesAttribute
CLASS io.undertow.servlet.handlers.security.SSLInformationAssociationHandler
METHOD handleRequest(HttpServerExchange)
AT LINE 127
IF true
DO log($CLASS, "DEBUG", "Method handleRequest, adding X509 certificates to request: \n" 
		+ "javax.servlet.request.X509Certificate = " + $certs
		)
ENDRULE

RULE logSSLInformationAssociationHandler.handleRequest.NoSSL
CLASS io.undertow.servlet.handlers.security.SSLInformationAssociationHandler
METHOD handleRequest(HttpServerExchange)
AT LINE 131
IF $ssl == null
DO log($CLASS, "DEBUG", "Method handleRequest, SSLSessionInfo is null, SSL attributes and X509 certificates are not added to request.")
ENDRULE

RULE logServletAuthenticationCallHandler.handleRequest.next
CLASS io.undertow.servlet.handlers.security.ServletAuthenticationCallHandler
METHOD handleRequest(HttpServerExchange)
AT INVOKE io.undertow.server.HttpHandler.handleRequest(HttpServerExchange) 1
IF true
DO log($CLASS, "DEBUG", "Method handleRequest, request is successfully authenticated and exchange is not completed, continuing in handlers chain.")
ENDRULE

RULE logServletAuthenticationCallHandler.handleRequest.dispatch
CLASS io.undertow.servlet.handlers.security.ServletAuthenticationCallHandler
METHOD handleRequest(HttpServerExchange)
AT INVOKE io.undertow.server.HttpServerExchange.dispatch(HttpHandler) 1
IF true
DO log($CLASS, "DEBUG", "Method handleRequest, request is running in the IO thread, dispatching request to the given executor.")
ENDRULE

RULE logServletAuthenticationCallHandler.handleRequest.sendError
CLASS io.undertow.servlet.handlers.security.ServletAuthenticationCallHandler
METHOD handleRequest(HttpServerExchange)
AT INVOKE io.undertow.servlet.spec.HttpServletResponseImpl.sendError(int) 1
IF true
DO log($CLASS, "DEBUG", "Method handleRequest, response code >= 400 and exchange is not completed, " + 
						"sending an error response to the client using the specified status code and clearing the buffer.")
ENDRULE

#Logging info about JSP config
RULE logJSPConfig
CLASS org.wildfly.extension.undertow.JSPConfig
METHOD <init>
AT EXIT
IF true
DO log($CLASS, "DEBUG", "JSP configuration, initParams (null if jsp support is disabled ): " + $this.servletInfo.getInitParams())
ENDRULE